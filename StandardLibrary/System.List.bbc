using Primitives;
using System.Memory;

struct List<T>
{
    private int _capacity;
    private int _length;
    private T[]* _ptr;

    export inline int length() { return this._length; }

    export inline void clear() { this._length = 0; }

    export inline T[]* raw() { return this._ptr; }

    export T indexer_get(int index)
    {
        if (index < 0 || index >= this._length)
        { crash "Index out of range exception"; }
        return this._ptr[index];
    }

    export void indexer_set(int index, T value)
    {
        if (index < 0 || index >= this._length)
        { crash "Index out of range exception"; }
        this._ptr[index] = value;
    }

    export List<T>(int capacity)
    {
        this._capacity = capacity;
        this._length = 0;
        this._ptr = new T[capacity]*;
    }

    export List<T>*(int capacity)
    {
        this._capacity = capacity;
        this._length = 0;
        this._ptr = new T[capacity]*;
    }

    export List<T>()
    {
        this._capacity = 4;
        this._length = 0;
        this._ptr = new T[4]*;
    }

    export List<T>*()
    {
        this._capacity = 4;
        this._length = 0;
        this._ptr = new T[4]*;
    }

    export destructor()
    {
        if (this._ptr)
        { delete this._ptr; }
    }

    export void add(T item)
    {
        if (this._length + 1 > this._capacity)
        {
            if (this._capacity)
            { this.ensure_capacity(this._capacity + (this._capacity >> 1)); }
            else
            { this.ensure_capacity(4); }
        }

        if (!this._ptr)
        { crash "List not initialized"; }

        this._ptr[this._length] = item;
        this._length++;
    }

    export T remove(int index)
    {
        if (!this._ptr)
        { crash "List not initialized"; }

        if (index < 0 || index >= this._length)
        { crash "Index out of range exception"; }

        T removed = this._ptr[index];
        
        int length = this._length;
        for (int i = index + 1; i < length; i++)
        {
            this._ptr[i - 1] = this._ptr[i];
        }

        this._length--;
        return removed;
    }

    export void swapback(int index)
    {
        if (!this._ptr)
        { crash "List not initialized"; }

        if (index < 0 || index >= this._length)
        { crash "Index out of range exception"; }

        this._length--;
        this._ptr[index] = this._ptr[this._length];
    }

    void ensure_capacity(int capacity)
    {
        if (this._capacity < capacity)
        {
            var old = this._ptr;
            this._ptr = new T[capacity]*;
            this._capacity = capacity;

            if (old)
            {
                int length = this._length;
                for (int i = 0; i < length; i++)
                {
                    this._ptr[i] = old[i];
                }

                delete old;
            }
        }
    }
}

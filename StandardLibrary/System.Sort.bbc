using Primitives;

/*
 * Source: https://en.wikipedia.org/wiki/Quicksort
 */
template<T>
export inline void quicksort(T[]* array, int length)
{
    quicksort(array, 0, length - 1);
}

/*
 * Source: https://en.wikipedia.org/wiki/Quicksort
 */
template<T>
export void quicksort(T[]* array, int left, int right)
{
    if (left >= 0 && right >= 0 && left < right)
    {
        int p = partition(array, left, right);
        quicksort(array, left, p);
        quicksort(array, p + 1, right);
    }
}

/*
 * Source: https://en.wikipedia.org/wiki/Quicksort
 */
template<T>
int partition(T[]* array, int left, int right)
{
    T pivot = array[left];
    int i = left - 1;
    int j = right + 1;

    while (1)
    {
        i++;
        while (array[i] < pivot) i++;

        j--;
        while (array[j] > pivot) j--;

        if (i >= j) return j;

        T _temp1 = array[i];
        array[i] = array[j];
        array[j] = _temp1;
    }
}

/*
 * Source: https://en.wikipedia.org/wiki/Insertion_sort
 */
template<T>
export void insertsort(T[]* values, int length)
{
    int i = 1;
    while (i < length)
    {
        T x = values[i];
        int j = i;
        while (j > 0 && values[j - 1] > x)
        {
            values[j] = values[j - 1];
            j = j - 1;
        }
        values[j] = x;
        i = i + 1;
    }
}
